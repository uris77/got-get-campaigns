import com.eriwen.gradle.js.tasks.CombineJsTask
import com.eriwen.gradle.js.tasks.MinifyJsTask

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "io.ratpack:ratpack-gradle:0.9.8"
    classpath "com.eriwen:gradle-js-plugin:1.12.1"
    classpath 'com.bluepapa32:gradle-watch-plugin:0.1.3'
    classpath 'com.github.jengelman.gradle.plugins:gradle-processes:0.3.0'
  }
}

apply plugin: "io.ratpack.ratpack-groovy"
apply plugin: "idea"
apply plugin: "eclipse"
apply plugin: "com.eriwen.gradle.js"
apply plugin: 'com.github.johnrengelman.processes'

repositories {
  jcenter()
  maven { url "http://repo.springsource.org/repo" }
  maven { url "http://www.allanbank.com/repo" }
}

dependencies {
    springloaded "org.springframework:springloaded:1.2.0.RELEASE"
    compile ratpack.dependency("pac4j")
    compile "org.pac4j:pac4j-openid:1.5.1"
    compile "org.pac4j:pac4j-oauth:1.5.1"

    compile ratpack.dependency("jackson")
    runtime 'org.slf4j:slf4j-simple:1.7.7'

    compile 'org.mongodb:mongo-java-driver:2.12.3'
    compile 'com.gmongo:gmongo:1.3'
    compile 'com.bloidonia:groovy-stream:0.5.4'
    compile "com.allanbank:mongodb-async-driver:2.0.0"

    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    testRuntime "org.objenesis:objenesis:1.2"
}

task stage{
    dependsOn installApp
}


project(":assets"){
    apply plugin: "watch"

    task concatJs(type: CombineJsTask) {
        source = [ "./js/vendor/underscore.js", "./js/vendor/angular-route.js",
                   "./js/app/url_utils.js", "./js/app/utils.js", "./js/app/outlet_survey_service.js",
                   "./js/app/surveys_list.js", "./js//app/surveys_create.js", "./js/app/surveys.js",
                   "./js/app/locations/create.js", "./js/app/locations/list.js", "./js/app/locations/details.js",
                   "./js/app/locations/edit.js","./js/app/locations/locations.js",
                   "./js/app/non_traditional_outlets/gateway_service.js", "./js/app/non_traditional_outlets/list.js",
                   "./js/app/non_traditional_outlets/create.js",  "./js/app/non_traditional_outlets/edit.js",
                   "./js/app/non_traditional_outlets/non_traditional_outlet_survey.js",
                   "./js/app/hotspots/gateway_service.js", "./js/app/hotspots/list.js", "./js/app/hotspots/create.js",
                   "./js/app/hotspots/edit.js", "./js/app/hotspots/hotspots_surveys.js",
                   "./js/app/traditional_outlets/gateway_service.js", "./js/app/traditional_outlets/list.js",
                   "./js/app/traditional_outlets/create.js", "./js/app/traditional_outlets/edit.js", "./js/app/traditional_outlets/traditional_outlet_survey.js",
                   "./js/app/pasmo_crud_app.js"]

        dest = file("./compiled/pasmo.js")

    }

    task compressJs(type: MinifyJsTask) {
        dependsOn concatJs
        source = "./compiled/pasmo.js"
        dest = file("../src/ratpack/public/app/pasmo-min.js")
    }

    task copyTemplates(type: Copy) {
        from 'js/app/templates'
        into '../src/ratpack/public/app/templates'
    }

    watch {
        assets {
            files files("js/app")
            tasks "compressJs"
        }
        templates {
            files files("js/app/templates")
            tasks "copyTemplates"
        }
    }
}


run {
    Properties properties = new Properties(System.getProperties())
    try {
        FileInputStream inputStream = new FileInputStream(".env")
        properties.load(inputStream)
        System.setProperties(properties)
        System.getProperties().each { prop ->
            systemProperty prop.key, prop.value
        }
    }catch (FileNotFoundException ex) {
        println "Could not find env file"
        println "This is normal in production."
    } catch (Exception e) {
        e.printStackTrace()
    }
    if(System.getProperty("RATPACK_ENVIRONMENT") != "PRODUCTION") {
        dependsOn "assets:compressJs", "assets:copyTemplates"
    }
}
